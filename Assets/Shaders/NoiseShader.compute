#pragma kernel CSMain

RWTexture2D<float4> Result;
float2 _Resolution;
float4 _Time;

float random (float2 st) {
    return frac(sin(dot(st.xy,
                         float2(12.9898,78.233)))*
        43758.5453123);
}

float noise (in float2 _st) {
    float2 i = floor(_st);
    float2 f = frac(_st);

    // Four corners in 2D of a tile
    float a = random(i);
    float b = random(i + float2(1.0, 0.0));
    float c = random(i + float2(0.0, 1.0));
    float d = random(i + float2(1.0, 1.0));

    float2 u = f * f * (3.0 - 2.0 * f);

    return lerp(a, b, u.x) +
            (c - a)* u.y * (1.0 - u.x) +
            (d - b) * u.x * u.y;
}

#define NUM_OCTAVES 5

float fbm (in float2 _st) {
    float v = 0.0;
    float a = 0.5;
    float2 shift = float2(100.0f, 100.0f);
    // Rotate to reduce axial bias
    float2x2 rot = float2x2(cos(0.5), sin(0.5),
                    -sin(0.5), cos(0.50));
    for (int i = 0; i < 5; ++i) {
        v += a * noise(_st);
        _st = mul(rot, _st) * 2.0 + shift;
        a *= 0.5;
    }
    return v;
}

[numthreads(8,8,1)]
void CSMain (uint3 id : SV_DispatchThreadID)
{
    float2 st = id.xy / _Resolution;
    float3 color;
    
    float2 q = float2(0,0);
    q.x = fbm( st + 0.00*_Time.w);
    q.y = fbm( st + float2(1.0,1.0));
    
    float2 r = float2(0,0);
    r.x = fbm( st + 1.0*q + float2(1.7,9.2)+ 0.15*_Time.w);
    r.y = fbm( st + 1.0*q + float2(8.3,2.8)+ 0.126*_Time.w);
    
    float f = fbm(st+r);
    
    color = lerp(float3(0.101961,0.619608,0.666667),
               float3(0.666667,0.666667,0.498039),
               clamp((f*f)*4.0,0.0,1.0));
    
    color = lerp(color,
                float3(0,0,0.164706),
                clamp(length(q),0.0,1.0));
    
    color = lerp(color,
                float3(0.666667,1,1),
                clamp(length(r.x),0.0,1.0));
    
    Result[id.xy] = float4((f*f*f+.6*f*f+.5*f)*color,1.);
}
